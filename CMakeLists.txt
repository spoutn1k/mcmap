CMAKE_MINIMUM_REQUIRED(VERSION 3.0)

PROJECT(mcmap LANGUAGES CXX VERSION 3.0.1)

SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

OPTION(DEBUG_BUILD "Debug build" OFF)
OPTION(SNAPSHOT "Support snapshot versions" OFF)

IF(STATIC_BUILD)
    SET(CMAKE_FIND_LIBRARY_SUFFIXES ".a")
    SET(BUILD_SHARED_LIBS OFF)
    SET(CMAKE_EXE_LINKER_FLAGS "-static")
ENDIF()

ADD_DEFINITIONS(-DSPDLOG_FMT_EXTERNAL=1)

FIND_PACKAGE(PNG REQUIRED)
FIND_PACKAGE(ZLIB REQUIRED)
FIND_PACKAGE(fmt REQUIRED)
FIND_PACKAGE(spdlog REQUIRED)
FIND_PACKAGE(OpenMP)
FIND_PACKAGE(GTest)
FIND_PACKAGE(Qt5 COMPONENTS Widgets LinguistTools)

SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)

INCLUDE_DIRECTORIES(src/include)

IF (NOT WIN32)
    ADD_DEFINITIONS(-Wall -Wextra -pedantic)
ENDIF()

ADD_DEFINITIONS(-D_FILE_OFFSET_BITS=64)

IF(DEBUG_BUILD)
	ADD_DEFINITIONS(-O0 -g3)
ELSE()
	ADD_DEFINITIONS(-O3)
ENDIF()

IF(SNAPSHOT)
    ADD_DEFINITIONS(-DSNAPSHOT_SUPPORT)
ENDIF()

IF(WIN32)
    # 100M stack + 3.5G heap on windows
    SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /STACK:104857600 /HEAP:3758096384")
    ADD_DEFINITIONS(-D_WINDOWS=1)
ENDIF()

IF(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  IF(CMAKE_CXX_COMPILER_VERSION VERSION_LESS "9.0.0")
    ADD_LINK_OPTIONS(-lstdc++fs)
  ENDIF()
ENDIF()

# Get file URL DEST MD5
FUNCTION(GET_FILE)
    FILE(
        DOWNLOAD
        ${ARGV0}
        ${ARGV1}
        EXPECTED_HASH ${ARGV2}
        STATUS DL_STATUS
    )

    LIST(GET DL_STATUS 0 _STATUS)

    IF(_STATUS)
        LIST(GET DL_STATUS 1 _ERROR)
        MESSAGE("Error downloading ${ARGV0}: ${_ERROR}")
    ELSE()
        MESSAGE("-- Successfully downloaded ${ARGV0}")
    ENDIF()
ENDFUNCTION()


ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(scripts)
ADD_SUBDIRECTORY(tests)
